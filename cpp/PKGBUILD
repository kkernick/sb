pkgname=sb++-git
pkgdesc="Sandbox Applications"
pkgver=r422.29efd7a
pkgrel=1

license=("Unlicense")
source=("git+https://github.com/kkernick/sb.git")
b2sums=("SKIP")
depends=(findutils glibc which bubblewrap libb2 libseccomp libexec)
makedepends=(git lld clang llvm doxygen)
optdepends=(
  'strace: for --strace, useful for debugging'
  'hardened_malloc: for --hardened-malloc'
  'zram-generator: for --sof=zram'
  'xdg-dbus-proxy: for --portals, --see, --talk, --own for Flatpak Emulation'
  'gocryptfs: for --encrypt'
  'kdialog: for --encrypt'
  'xorg-server-xephyr: For Xorg applications and --xorg'
  'fd: For --stats'

)
arch=("any")
provides=("sb")
conflicts=("sb" "sb-git")

pkgver() {
  cd $srcdir/sb
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

prepare() {
  cd $srcdir/sb/cpp
  git submodule init
  git submodule update
}

build() {
  cd $srcdir/sb/cpp
  make
  make doxygen
}

package() {
  cd $srcdir/sb/cpp
  for binary in sb-refresh sb-seccomp; do
    install -Dm755 "$binary" "$pkgdir/usr/bin/$binary"
  done

  install -Dm755 sb "$pkgdir/usr/bin/sb"
  install -Dm777 -d "$pkgdir/usr/share/sb"

  install -Dm644 "$srcdir/sb/cpp/sb.service" "$pkgdir/usr/lib/systemd/user/sb.service"
  install -Dm644 "$srcdir/sb/cpp/sb.conf" "$pkgdir/usr/lib/systemd/zram-generator.conf.d/sb.conf"
  install -Dm755 "$srcdir/sb/sb-open" "$pkgdir/usr/bin/sb-open"

  for l in "$srcdir/sb/LICENSE" $srcdir/sb/cpp/src/third_party/thread-pool/LICENSE.txt; do
    install -Dm644 "$l" "$pkgdir/usr/share/licenses/sb/$(basename $l)"
  done
}

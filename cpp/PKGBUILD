pkgname=sb++-git
pkgdesc="Sandbox Applications"
pkgver=r377.f20b610
pkgrel=1

source=(
  "git+https://github.com/kkernick/sb.git"
  "sb.sysusers"
)
b2sums=(
  "SKIP"
  "792af0991e641558928d35ff55f721507813fe3433b15e3031fa7e9fa76acb8ebde638154a97286bf515a9397087e4a605e925e07fc0505fba8f80fddd90fb91"
)
depends=(findutils glibc which bubblewrap)
makedepends=(git lld libb2 clang llvm doxygen)
optdepends=(
  'strace: for --strace, useful for debugging'
  'hardened_malloc: for --hardened-malloc'
  'zram-generator: for --sof=zram'
  'xdg-dbus-proxy: for --portals, --see, --talk, --own for Flatpak Emulation'
  'libseccomp: for --syscalls'
)
arch=("any")
provides=("sb")
conflicts=("sb" "sb-git")

pkgver() {
  cd $srcdir/sb
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

build() {
  cd $srcdir/sb/cpp
  make cpp
  make doxygen
}

package() {
  cd $srcdir/sb/cpp
  for binary in sb-refresh sb-seccomp; do
    install -Dm755 "$binary" "$pkgdir/usr/bin/$binary"
  done

  install -Dm2755 -g77 sb "$pkgdir/usr/bin/sb"
  install -Dm775 -g77 -d "$pkgdir/usr/share/sb"

  install -Dm644 "$srcdir/sb/cpp/sb.service" "$pkgdir/usr/lib/systemd/user/sb.service"
  install -Dm644 "$srcdir/sb/cpp/sb.conf" "$pkgdir/usr/lib/systemd/zram-generator.conf.d/sb.conf"
  install -Dm755 "$srcdir/sb/sb-open" "$pkgdir/usr/bin/sb-open"
  install -Dm644 "$srcdir/sb/sb.sysusers" "$pkgdir/usr/lib/sysusers.d/sb.conf"


  for page in $srcdir/sb/cpp/docs/man/man3/*; do
    gzip $page
    install -Dm644 "$page.gz" "$pkgdir/usr/share/man/man3/sb.$(basename $page).gz"
  done

  for license in "$srcdir/sb/LICENSE" $srcdir/sb/cpp/src/third_party/licenses/*; do
    install -Dm644 "$license" "$pkgdir/usr/share/licenses/sb/$(basename $license)"
  done
}

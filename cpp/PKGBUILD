pkgname=sb++-git
pkgdesc="Sandbox Applications"
pkgver=r345.22074db
pkgrel=1

source=("git+https://github.com/kkernick/sb.git")
sha256sums=("SKIP")
depends=(findutils glibc which bubblewrap)
makedepends=(git lld libb2 clang llvm doxygen)
optdepends=(
  'strace: for --strace, useful for debugging'
  'hardened_malloc: for --hardened-malloc'
  'zram-generator: for --sof=zram'
  'xdg-dbus-proxy: for --portals, --see, --talk, --own for Flatpak Emulation'
  'libseccomp: for --syscalls'
)
arch=("any")
provides=("sb")
conflicts=("sb" "sb-git")

pkgver() {
  cd $srcdir/sb
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

build() {
  cd $srcdir/sb/cpp
  make cpp
  make doxygen
}

package() {
  cd $srcdir/sb/cpp
  for binary in sb sb-startup sb-refresh sb-seccomp; do
    install -Dm755 "$binary" "$pkgdir/usr/bin/$binary"
  done

  cd $srcdir/sb
  for service in sb.service; do
    install -Dm644 "$service" "$pkgdir/usr/lib/systemd/user/$service"
  done

  install -Dm 644 sb.conf "$pkgdir/usr/lib/systemd/zram-generator.conf.d/sb.conf"
  install -Dm755 "$srcdir/sb/sb-open" "$pkgdir/usr/bin/sb-open"
  
  for page in $srcdir/sb/cpp/docs/man/man3/*; do
    gzip $page
    install -Dm644 "$page.gz" "$pkgdir/usr/share/man/man3/sb.$(basename $page).gz"
  done
  
  for license in "$srcdir/sb/LICENSE" $srcdir/sb/cpp/src/third_party/licenses/*; do
    install -Dm644 "$license" "$pkgdir/usr/share/licenses/sb/$(basename $license)"
  done
}
